buildscript {

    repositories {
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "com.github.ntrrgc:ts-generator:1.1.1"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id "ch.ayedo.ktsgenerator" version "2.1.0"
}

group 'ayedo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

typescriptGenerator {

    // The path to the resulting generated type definitions file
    outputPath = project.projectDir.toPath().resolve("src/typings/typings.d.ts") // required, this is an example

    // The classpath to scan for classes to convert to typescript definitions
    classPath = layout.files(getProject().sourceSets.main.runtimeClasspath) // required, this is an example

    // Only Kotlin classes from this package will be considered for generation
    packageName = "ch.ayedo.ktsgeneratorexample" // required, this is an example

    // Only classes which have the following postfixes will be generated
    postfixFilters = ["OP", "IP"] // optional, the default is []

    // Which type to use for int
    intTypeName = "number" // optional, this is the default

    // Custom type mappings
    typeMappings = [
            "java.time.LocalDateTime": 'Moment',
            "java.time.LocalDate"    : 'DateMoment',
            "java.time.LocalTime"    : 'TimeMoment',
            "java.util.UUID"         : 'string',
            "java.net.URL"           : 'string',
            "java.math.BigDecimal"   : 'BigNumber'] // optional, the default is []

    // Imports to be added at the top of the generated typescript definitions file
    imports = ["import { Moment } from 'moment';",
               "import BigNumber from 'bignumber.js';"] // optional, the default is []

    // What to use for void type.
    // Options: 'NULL', or 'UNDEFINED'
    voidType = "UNDEFINED" // optional, this is the default

}
